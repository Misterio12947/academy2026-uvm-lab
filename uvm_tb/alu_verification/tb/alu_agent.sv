//------------------------------------------------------------------------------
// alu_agent.sv
// Agent activo por default. Encapsula driver, monitor y sequencer.
//------------------------------------------------------------------------------
typedef uvm_sequencer#(alu_transaction) alu_sequencer;

class alu_agent extends uvm_agent;

    `uvm_component_utils(alu_agent)

    alu_driver     drv;
    alu_monitor    mon;
    alu_sequencer  sqr;

    function new(string name, uvm_component parent);
        super.new(name, parent);
    endfunction

    function void build_phase(uvm_phase phase);
        super.build_phase(phase);
        mon = alu_monitor::type_id::create("mon", this);
        if (get_is_active() == UVM_ACTIVE) begin
            drv = alu_driver::type_id::create("drv", this);
            sqr = alu_sequencer::type_id::create("sqr", this);
        end
    endfunction

    function void connect_phase(uvm_phase phase);
        super.connect_phase(phase);
        if (get_is_active() == UVM_ACTIVE)
            drv.seq_item_port.connect(sqr.seq_item_export);
    endfunction

endclass
